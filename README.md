# FileId

I wrote this to manage my screenshot collection. I have nearly a hundred thousand of them and sometimes I'd like to find a screenshot which was taken nearest to a specific date-time. In order to accomplish that, I had to get the file IDs of all of my screenshots, cram them into a little index file alongside a date, search for that date in the index and get the path of the corresponding screenshot to show it.

Minimal example that shows a message box with one of the full paths of "test.ahk".
```ahk
#include <FileId>
FilePathToIndex("test.ahk", &high, &low, &handle)
MsgBox FileIdToPath(handle, "C", high, low)
DllCall("CloseHandle", "Ptr", handle) || MsgBox("CloseHandle failed " A_LastError)
```

Code that built an unsorted index from all of my existing screenshots.
```ahk
#include <FileId>

; Goes through all files in the directory A_Args[1] and dumps their time-since-epoch and file ID into a file.

epoch := "20130101000000"

outfile := FileOpen("logid-unsorted", 0x1)

Loop Files A_Args[1] "\*", "FR"
{
	if A_LoopFileName ~= "^20\d\d\d\d\d\d\d\d\d\d\d\d"
	{
		outfile.WriteUInt(DateDiff(SubStr(A_LoopFileName, 1, 14), epoch, "Seconds"))
		FilePathToIndex("\\?\" A_LoopFileFullPath, &high, &low) || (MsgBox("\\?\" A_LoopFileFullPath), ExitApp())
		outfile.WriteUInt(high)
		outfile.WriteUInt(low)
	}
}
```

Lua (Luvit) script that sorts the output of the above script.
```lua
-- Sorts a time-high-low triplets file generated by time2-index-collect.ahk

local fs = require "fs"
local infile = fs.readFileSync "logid-unsorted"
local list = {}
for qwe in infile:gmatch("............") do
	-- get little-endian UInt from the start
	table.insert(list, {string.unpack("<L", qwe), qwe})
end
table.sort(list, function(a, b) return a[1] < b[1] end)
for i,v in ipairs(list) do list[i] = v[2] end
fs.writeFileSync("logid", table.concat(list))
```

Code to query a date from a file full of sorted <time, high, low> triplets.
```ahk
#include <FileId>

idlogepoch := "20130101000000"
scrdir := A_MyDocuments "\Screenshots" ; root directory
idlogpath := scrdir "\logid"

MsgBox GetPathFromDate("20230201010017")

GetPathFromDate(timestamp)
{
	try
		t := DateDiff(timestamp, idlogepoch, "Seconds")
	catch
		return 0
	logid := FileOpen(idlogpath, 0)
	, a := 0
	, b := logid.Length // 12
	, c := (a + b) >> 1
	Loop
	{
		logid.Seek(c * 12)
		t2 := logid.ReadInt()
		; MsgBox t " " t2 "`n" a " " c " " b "`n" (name := FileIdToPath(logid.Handle, "C", logid.ReadUInt(), logid.ReadUInt()))
		if t2 == t || (b - a) < 3
			break
		if t2 < t
			a := c
		else
			b := c
		c := (a + b) >> 1
	}
	name := FileIdToPath(logid.Handle, "C", logid.ReadUInt(), logid.ReadUInt())
	
	; Ignore deleted or trashed files
	while (!name || name ~= "^.:\\\$Recycle.Bin\\") && c > 0
		logid.Seek(--c * 12 + 4), name := FileIdToPath(logid.Handle, "C", logid.ReadUInt(), logid.ReadUInt())
	
	logid.Close()
	
	return name
}
```

This is me just finishing something and then dumping it onto the [AutoHotkey forum](https://www.autohotkey.com/boards/viewtopic.php?f=83&t=113693) just in case it's useful to anyone else.
